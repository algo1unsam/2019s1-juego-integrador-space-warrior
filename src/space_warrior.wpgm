import wollok.game.*
import nave.*
import enemigos.*
import enemigosMalos.*
import disparo.*

program space_warrior {
	
	game.title("SpaceWarrior")
	game.height(12)
	game.width(14)
	game.ground("ground.png")
	game.addVisual(nave)
	game.say(nave, "HOLA!")
	game.say(nave, "PRESIONA LA BARRA ESPACIADORA PARA COMENZAR!")
	
	const enemigos = [ new Enemigo(position = game.at(1,11), pasos=1), 
		new Enemigo(position = game.at(3,11)), 
		new Enemigo(position = game.at(5,11)), 
		new Enemigo(position = game.at(6,11), pasos=3), 
		new Enemigo(position = game.at(8,11), pasos=3), 
		new Enemigo(position = game.at(10,11))
	]
	
	const enemigosEnPantalla = { =>
		enemigos.forEach{ enemigo => game.addVisual(enemigo)}
		enemigos.forEach{ enemigo => game.hideAttributes(enemigo)}
	}
	const colisiones = { =>
		const jugadores = []
		jugadores.addAll(enemigos)
		jugadores.add(nave)
		jugadores.forEach{ jugador => game.whenCollideDo(jugador, { disparo => disparo.impactar(jugador)})}
	}
	const movimientoNave = { =>
		keyboard.left().onPressDo{ nave.position(nave.position().left(1))}
		keyboard.right().onPressDo{ nave.position(nave.position().right(1))}
		keyboard.up().onPressDo{ nave.disparoInicial()}
	}
	const perderJuego = { => game.onTick(1000, "perderJuego", { =>
		if (nave.estaMuerto()) {
			nave.finDelJuego()
		}
	}) }
	const accionesNave = { =>
		movimientoNave.apply()
		colisiones.apply()
		perderJuego.apply()
	}
	const pasarDeNivel = { =>
		game.clear()
		game.addVisual(nave)
		nave.subirNivel()
		nave.muertes(0)
		if (nave.nivel() < 4) {
			game.say(nave, "NIVEL" + nave.nivel())
		}
	}
	const accionesEnemigos = { segs_desplazamiento , segs_disparos =>
		game.onTick(segs_desplazamiento, "comenzarDesplazamiento", { => enemigos.forEach{ enemigo => enemigo.desplazarse()}})
		game.onTick(segs_disparos, "comenzarDisparos", { => enemigos.forEach{ enemigo => enemigo.disparoInicial()}})
		game.onTick(1000, "quitarEnemigosMuertos", { => enemigos.removeAllSuchThat{ enemigo => enemigo.estaMuerto()}})
	}
	
	enemigosEnPantalla.apply()
	
	keyboard.space().onPressDo{ 
		
		accionesEnemigos.apply(4000, 7000)
		accionesNave.apply()
		game.onTick(1000, "pasarANivel2", {=>
			if (nave.nivel() == 1 and (nave.muertes() == 6)) {
				pasarDeNivel.apply()
				enemigos.add(new EnemigoMalo(position = game.at(2, 11), pasos = 1, msegs = 3000))
				enemigos.add(new EnemigoMalo(position = game.at(4, 11), pasos = 2, msegs = 3000))
				enemigos.add(new EnemigoMalo(position = game.at(6, 11), pasos = 3, msegs = 3000))
				enemigos.add(new EnemigoMalo(position = game.at(8, 11), pasos = 2, msegs = 3000))
				enemigos.add(new EnemigoMalo(position = game.at(10, 11), pasos = 1, msegs = 3000))
				enemigosEnPantalla.apply()
				accionesNave.apply()
				accionesEnemigos.apply(10000, 6000)
				pasarANivel3.apply()
			}
		})
	}
	
	const pasarANivel3 = {=> game.onTick(1000, "pasarANivel3", {=>
		if (nave.nivel() == 2 and (nave.muertes() == 5)) {
			pasarDeNivel.apply()
			enemigos.add(new EnemigoMalo(position = game.at(2, 11), pasos = 1, msegs = 3000))
			enemigos.add(new EnemigoMalo(position = game.at(9, 11), pasos = 1, msegs = 3000))
			enemigos.add(new EnemigoMuyMalo(position = game.at(5, 9), pasos = 1, msegs = 3000))
			enemigosEnPantalla.apply()
			accionesNave.apply()
			accionesEnemigos.apply(5000, 9000)
			finDelJuego.apply()
		}
	}) }
	
	const finDelJuego = {=> game.onTick(1000, "finDelJuego", {=>
		if (nave.nivel() == 3 and (nave.muertes() == 3)) {
			pasarDeNivel.apply()
			game.say(nave, "GANASTE")
			game.say(nave, "CHAU!")
			nave.finDelJuego()
		}
	}) }
	game.errorReporter(nave)
	game.start()
}
